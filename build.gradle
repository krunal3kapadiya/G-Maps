// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.2.40'
    ext.applicationID = 'com.krunal3kapadiya.gmaps'

    ext.versions = [
            'minSdk'            : 19,
            'compileSdk'        : 27,

            'buildTools'        : '27.1.1',
            'androidPlugin'     :   '3.1.2',
            'kotlin'            : '1.2.20',
            'archComp'          : '1.1.1',
            'livedataTesting'   : '1.1.0',
            'room'              : '1.0.0',
            'roomRx'            : '1.0.0',
            'roomTesting'       : '1.0.0',
            'constraint'        : '1.1.0',
            'gms'               : '3.2.1',
            'dagger'            : '2.12',
            'gson'              : '2.8.4',
            'retrofit'          : '2.3.0',
            'retrofitRxAdapter' : '1.0.0',
            'okhttp'            : '3.9.0',
            'okhttpDownloader'  : '1.1.0',
            'picasso'           : '2.5.2',
            'rxJava'            : '2.1.7',
            'rxAndroid'         : '2.0.1',
            'stetho'            : '1.5.0',
            'junit'             : '4.12',
            'atsl'              : '1.0.1',
            'playServiceVersion': '11.8.0',
            'mockitoKotlin'     : '1.5.0',
            'robolectric'       : '3.6.1',
            'sourceCompat'      : JavaVersion.VERSION_1_8,
            'targetCompat'      : JavaVersion.VERSION_1_8
    ]


    ext.deps = [
            android    : [
                    'gradlePlugin'   : "com.android.tools.build:gradle:${versions.androidPlugin}",
                    'lifecycleExt'   : "android.arch.lifecycle:extensions:${versions.archComp}",
                    'lifecycleCommon': "android.arch.lifecycle:common-java8:${versions.archComp}",
                    'roomRuntime'    : "android.arch.persistence.room:runtime:${versions.room}",
                    'roomCompiler'   : "android.arch.persistence.room:compiler:${versions.room}",
                    'roomRx'         : "android.arch.persistence.room:rxjava2:${versions.roomRx}",
            ],
            support    : [
                    'appCompat'    : "com.android.support:appcompat-v7:${versions.buildTools}",
                    'recyclerView' : "com.android.support:recyclerview-v7:${versions.buildTools}",
                    'cardView'     : "com.android.support:cardview-v7:${versions.buildTools}",
                    'support'      : "com.android.support:support-v4:${versions.buildTools}",
                    'designSupport': "com.android.support:design:${versions.buildTools}",
                    'constraint'   : "com.android.support.constraint:constraint-layout:${versions.constraint}",
            ],
            kotlin     : [
                    'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
                    'stdlib'      : "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}",
            ],
            reactivex  : [
                    'rxJava'   : "io.reactivex.rxjava2:rxjava:${versions.rxJava}",
                    'rxAndroid': "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"
            ],
            google     : [
                    'gmsPlugin'      : "com.google.gms:google-services:${versions.gms}",
                    'dagger'         : "com.google.dagger:dagger:${versions.dagger}",
                    'daggerProcessor': "com.google.dagger:dagger-compiler:${versions.dagger}",
                    'gson'           : "com.google.code.gson:gson:${versions.gson}",
            ],
            square     : [
                    'retrofit'         : "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    'gsonConverter'    : "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                    'okhttp'           : "com.squareup.okhttp3:okhttp:${versions.okhttp}",
                    'picasso'          : "com.squareup.picasso:picasso:${versions.picasso}",
                    'okhttpDownloader' : "com.jakewharton.picasso:picasso2-okhttp3-downloader:${versions.okhttpDownloader}",
                    'retrofitRxAdapter': "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:${versions.retrofitRxAdapter}",
            ],
            facebook   : [
                    'stetho'            : "com.facebook.stetho:stetho:${versions.stetho}",
                    'networkInterceptor': "com.facebook.stetho:stetho-okhttp3:${versions.stetho}",
                    'facebookSDK'       : "com.facebook.android:facebook-android-sdk:4.7.0"
            ],
            test       : [
                    'junit'          : "junit:junit:${versions.junit}",
                    'atslRunner'     : "com.android.support.test:runner:${versions.atsl}",
                    'atslRules'      : "com.android.support.test:rules:${versions.atsl}",
                    'mockitoKotlin'  : "com.nhaarman:mockito-kotlin:${versions.mockitoKotlin}",
                    'robolectric'    : "org.robolectric:robolectric:${versions.robolectric}",
                    'mockWebServer'  : "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
                    'roomTesting'    : "android.arch.persistence.room:testing:${versions.roomTesting}",
                    'livedataTesting': "android.arch.core:core-testing:${versions.livedataTesting}",
            ],
            firebaseLib: [
                    fcm     : "com.google.firebase:firebase-messaging:${versions.playServiceVersion}",
                    auth    : "com.google.firebase:firebase-auth:${versions.playServiceVersion}",
                    database: "com.google.firebase:firebase-database:${versions.playServiceVersion}",
                    ads     : "com.google.firebase:firebase-ads:${versions.playServiceVersion}",
                    config  : "com.google.firebase:firebase-config:${versions.playServiceVersion}",
            ],
            timber     : 'com.jakewharton.timber:timber:4.5.1',
            multidex    : 'com.android.support:multidex:1.0.2',

    ]

    ext.packageExcludes = {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    ext.lintOption = {
        abortOnError false
        htmlReport true
        xmlReport true
        disable 'InvalidPackage'
    }

    ext.aaptOption = {
        cruncherEnabled false
    }

    ext.dexOption = {
        preDexLibraries true
        maxProcessCount 6
    }

    ext.excludeGrp = {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'cardview-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.jakewharton.timber', module: 'timber'
        exclude group: 'org.apache.httpcomponents'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.android.gradlePlugin
        classpath deps.google.gmsPlugin
        classpath deps.kotlin.gradlePlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}